{
    --------------------------------------------
    Filename: RF-waterfall-CC2500.spin2
    Description: Display received power levels in the frequency domain
        as a "waterfall"
        Utilizes ISM-band transceiver IC's (CC2500 version)
    Author: Jesse Burt
    Copyright (c) 2023
    Started: Apr 28, 2020
    Updated: Jul 17, 2023
    See end of file for terms of use.
    --------------------------------------------
}
CON

' --
    _clkfreq    = 250_000_000
    _xtlfreq    = 20_000_000

    SER_BAUD    = 2_000_000

    VGA_BASEPIN = 32                        ' 0, 8, 16, 24, 32, 40, 48
' --

    WIDTH       = 320
    HEIGHT      = 240
    XMAX        = WIDTH-1
    YMAX        = HEIGHT-1
    BUFFSZ      = WIDTH * HEIGHT

    LABEL_COL   = 0
    DATA_COL    = 11


VAR

    long _basefreq, _span
    long _dly

    word _wf_x, _wf_y, _wf_width, _wf_height
    byte _framebuffer[BUFFSZ]

    byte _offset
    byte _strtemp[14]

OBJ

    cfg:    "boardcfg.p2eval"
    ser:    "com.serial.terminal.ansi"
    vga:    "display.vga.bitmap-8bpp"
    fnt:    "font.5x8"
    cc2500: "wireless.transceiver.cc2500" | CS=0, SCK=1, MOSI=2, MISO=3, SPI_FREQ=6_500_000
    str:    "string"

PUB main() | lna, dvga, cmd, rxbw, intfreq

    vga.set_timings(10, 33, 16, 89, 85, 640)
    full_spec_palette()
'    grey_scale_palette
    setup()

    _basefreq := 2_400_000                      ' 2_400_000..2_483_500 (kHz)
    _span := 1_000_000                          ' Freq span from base freq

    cc2500.idle()                                 ' Change to Idle while changing settings
    cc2500.auto_cal_mode(cc2500#IDLE_RXTX)
    cc2500.carrier_freq(_basefreq)
    cc2500.syncwd_mode(cc2500#SYNCMODE_3032_CS)
    cc2500.carrier_sense_thresh(14)
    cc2500.dvga_gain(-3)
    cc2500.lna_gain(-17)
    cc2500.rx_bw(58)
    cc2500.interm_freq(380_859)
    cc2500.agc_mode(cc2500#AGC_OFF)                              ' AGC_NORMAL, AGC_OFF
    cc2500.dc_block_ena(FALSE)
    cc2500.rx_mode()

    lna := dvga := rxbw := 0
    intfreq := 380859
    _offset := 138 #> 138

    { waterfall position, width, height }
    _wf_x := 0
    _wf_y := vga.font_height() * 6              ' snap to text rows
    _wf_width := _wf_x+300
    _wf_height := _wf_y+(vga.font_height() * 12)-4

    { Draw frame around waterfall }
    vga.box(_wf_x, _wf_y, _wf_width, _wf_height, 255, FALSE)
    display_settings()

    { Draw the color scale (for RSSI) }
    draw_scale(_wf_width+5, _wf_y, _wf_height-_wf_y)
    repeat
        waterfall(_wf_x, _wf_y, _wf_width, _wf_height, _basefreq, _span)
        if ( lookdown(cmd := ser.rx_check(): 32..127) )
            case cmd
                "=":
                    lna := (lna + 1) <# 7
                    cc2500.lna_gain(lookupz(lna: -17, -14, -11, -9, -7, -6, -2, 0))
                "-":
                    lna := (lna - 1) #> 0
                    cc2500.lna_gain(lookupz(lna: -17, -14, -11, -9, -7, -6, -2, 0))
                "]":
                    dvga := (dvga + 1) <# 7
                    cc2500.dvga_gain(lookupz(dvga: -3, -2, -1, 0))
                "[":
                    dvga := (dvga - 1) #> 0
                    cc2500.dvga_gain(lookupz(dvga: -3, -2, -1, 0))
                ";":
                    _offset := (_offset - 1) #> 138
                "'":
                    _offset := (_offset + 1) <# 255
                ".":
                    rxbw := (rxbw + 1) <# 15
                    cc2500.rx_bw(lookupz(rxbw: 58, 68, 81, 102, 116, 135, 162, 203, 232, 270, ...
                                                325, 406, 464, 541, 650, 812))
                ",":
                    rxbw := (rxbw - 1) #> 0
                    cc2500.rx_bw(lookupz(rxbw: 58, 68, 81, 102, 116, 135, 162, 203, 232, 270, ...
                                                325, 406, 464, 541, 650, 812))
                "p":
                    intfreq := (intfreq + 25390) <# 787109
                    cc2500.interm_freq(intfreq)
                "o":
                    intfreq := (intfreq - 25390) #> 25390
                    cc2500.interm_freq(intfreq)
                "s":
                    _span := (_span - 10_000) #> 10_000
                "S":
                    _span := (_span + 10_000) <# 5_000_000
                "b":
                    _basefreq := (_basefreq - 500) #> 2_400_000
                "B":
                    _basefreq := (_basefreq + 500) <# 2_483_500
                "d":
                    _dly := (_dly-1) #> 0
                    ser.printf("%d\n", _dly)

                "D":
                    _dly := (_dly+1) <# 1000
                    ser.printf("%d\n", _dly)

                other:
            cc2500.rx_mode()
            display_settings()                  ' Update the settings display

PUB display_settings() | basefreq, span, lna, dvga, rxbw, ifreq, startrow

    lna := cc2500.lna_gain()
    dvga := cc2500.dvga_gain()
    rxbw := cc2500.rx_bw()
    ifreq := cc2500.interm_freq()

    vga.fgcolor(255)
    vga.pos_xy(0, 0)

    vga.printf(@"%skHz   ", dec_sep(_basefreq, ","))
    vga.printf(@"Span: %sHz", dec_sep(_span, ","))

    startrow := ((_wf_y + _wf_height+4) / vga.font_height())

    vga.pos_xy(LABEL_COL, startrow)
    vga.printf(@"LNA Gain: ")
    vga.pos_xy(DATA_COL, startrow)
    vga.printf(@"%ddB ", lna)

    vga.pos_xy(LABEL_COL, startrow+1)
    vga.printf(@"DVGA Gain: ")
    vga.pos_xy(DATA_COL, startrow+1)
    vga.printf(@"%ddB ", dvga)

    vga.pos_xy(LABEL_COL, startrow+2)
    vga.printf(@"RX BW: ")
    vga.pos_xy(DATA_COL, startrow+2)
    vga.printf(@"%dkHz  ", rxbw)

    vga.pos_xy(LABEL_COL, startrow+3)
    vga.printf(@"IF: ")
    vga.pos_xy(DATA_COL, startrow+3)
    ifnot ( (ifreq / 1000) > (rxbw / 2) )
        vga.fgcolor(190)
    else
        vga.fgcolor(255)
    vga.printf(@"%dHz ", ifreq)
    vga.fgcolor(255)

PUB waterfall(sx, sy, ex, ey, base_freq, span) | x, y, left, top, bottom, right, c, freqstep

    left := sx + 1
    top := sy + 1
    bottom := ey - 1
    right := ex - 1
    freqstep := span / (ex-sx)                                  ' *CC2500 Freq resolution is actually 397Hz
' span = 1_000_000
' sx = 0
' ex = 300
' --------
' freqstep = 3333 (/ 1000 = 3.333)
' left = 1
' base_freq = 2_400_000
' (2_400_000 + (3 * (0-1)))
    vga.wait_vsync()
    repeat x from left to right
        cc2500.carrier_freq(base_freq + (freqstep * (x-left)) / 1000)
'        time.USleep(_dly)
        c := (cc2500.rssi()+_offset)
        vga.plot(x, top, c)
{
        y := sy-(c/3)+5                                         ' Plot above waterfall
        vga.Plot(x, y-1 <# (sy-2), 127)                         '
        vga.Plot(x, y-2 <# (sy-2), 0)                           '
        vga.Line(x, sy-1, x, y <# (sy-2), 0)                    '
}
    vga.scroll_down(left, top, right, bottom)

PRI draw_scale(x, y, ht) | idx, color, scl_width, bottom, top, range
' Draw the color scale setup at program start
    range := bottom := y+ht
    top := y
    scl_width := 5

    repeat idx from bottom to top
        color := (range-idx) * 2                                ' Skip every other color in the scale
        vga.line(x, idx, x+scl_width, idx, color)               '   so most of it can be fit on screen

PRI dec_sep(n, sep) | bills, mills, thous, hunds
' Display a decimal number (unsigned int) with thousands separators
'   n:          0..2147483647
'   sep_char:   32..127 (ASCII character)
    bytefill(@_strtemp, 0, 10)
    if ( n >= 1_000_000_000 )
        bills := (n / 1_000_000_000 )
        mills := (n // 1_000_000_000 ) / (1_000_000)
        thous := ((n // 1_000_000_000 ) // (1_000_000)) / 1_000
        hunds := ((n // 1_000_000_000 ) // (1_000_000)) // 1_000
        str.sprintf7(@_strtemp, @"%d%c%03.3d%c%03.3d%c%03.3d",  bills, sep, ...
                                                                mills, sep, ...
                                                                thous, sep, ...
                                                                hunds)
    elseif ( n >= 1_000_000 )
        mills := (n / 1_000_000)
        thous := ((n // 1_000_000 ) / (1_000))
        hunds := ((n // 1_000_000 ) // (1_000))
        str.sprintf5(@_strtemp, @"%d%c%03.3d%c%03.3d",  mills, sep, ...
                                                        thous, sep, ...
                                                        hunds)
    elseif ( n >= 1_000 )
        thous := (n / 1_000 )
        hunds := (n // 1_000)
        str.sprintf3(@_strtemp, @"%d%c%03.3d",  thous, sep, ...
                                                hunds)
    else
        str.sprintf1(@_strtemp, @"%d", hunds)

    return @_strtemp

PUB setup()

    ser.start(SER_BAUD)
    ser.clear()
    ser.strln(@"Serial terminal started")

    if ( vga.start(VGA_BASEPIN, WIDTH, HEIGHT, @_framebuffer) )
        vga.font_addr(fnt.ptr())
        vga.font_scl(1)
        vga.font_spacing(1, 0)
        vga.font_sz(fnt.WIDTH, fnt.HEIGHT)
        vga.clear()
        ser.strln(@"VGA 8bpp driver started")
        vga.char_attrs(vga.DRAWBG)
    else
        ser.strln(@"VGA 8bpp driver failed to start")
        repeat

    if ( cc2500.start() )
        ser.strln(@"CC2500 driver started")
    else
        ser.strln(@"CC2500 driver failed to start - halting")
        repeat

PUB grey_scale_palette() | i, ptr_pal

    ptr_pal := vga.palette_ptr()
    repeat i from 0 to 255
        long[ptr_pal][i] := 0 | (i << 16) | (i << 8) | i

PUB full_spec_palette() | i, r, g, b, c, ptr_pal
' Set up palette
    r := g := b := c := 0
    ptr_pal := vga.palette_ptr()
    repeat i from 0 to 255
        c := 0 | (r << 16) | (g << 8) | b
        long[ptr_pal][i] := c
        case i
            0..31:
                r += 4
                g := 0
                b += 4
            32..63:
                r -= 4
                g := 0
                b := b
            64..95:
                r := 0
                g += 4
                b := b
            96..127:
                r := 0
                g := g
                b -= 4
            128..159:
                r += 4
                g := g
                b := b
            160..191:
                r := r
                g -= 4
                b := 0
            192..253:
                r := r
                g += 4
                b += 4
            254..255:
                r := g := b := 255

DAT
{
Copyright 2023 Jesse Burt

Permission is hereby granted, free of charge, to any person obtaining a copy of this software and
associated documentation files (the "Software"), to deal in the Software without restriction,
including without limitation the rights to use, copy, modify, merge, publish, distribute,
sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all copies or
substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT
NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM,
DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT
OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
}

